;	Hitachi SH Math Support
;	Copyright 1999 Metrowerks, Inc.
		
		.EXPORT     __l_divs
   		.SECTION    .text

;
;	unsigned int _l_divs(int I1, int I2)
;
;	Notes:
;	Runtime support routine.
;

__l_divs:	
		
;	_divs32q	-	Quotient of 32 bit / 32bit (Signed)
;	
;	Input: 	R4	divident
;			R5	divisor
;	Output:	R0	quotient
;			T	error flag (T=1 : error)
;
;	Notes:
; 	Divides the divident (signed 32 bits) by the divisor (signed 32 bits)
;	and determines the quotient (signed 32 bits). Also indicates and error
;   (division by 0) in the T bit.
;

_divs32q
		MOV		R5,R1
		MOV		R4,R0		; quotient
		
		TST		R1,R1		; divisor = 0 ?
		BT		DIVS32Q1	; yes
		XOR		R3,R3		; R3 = H'00000000
		
		DIV0S	R3,R0		; T bit = Sign of dividend
		SUBC	R2,R2
		SUBC	R3,R0
		DIV0S	R1,R2		; divide as signed
		
		ROTCL	R0			; divide 1 step
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2

		ROTCL	R0			
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		
		ROTCL	R0			
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2

		ROTCL	R0			
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2
		ROTCL	R0
		DIV1	R1,R2

		ROTCL	R0		
		ADDC	R3,R0

		RTS
		CLRT				; T bit = no error
DIVS32Q1
		RTS
		SETT				; T bit = error

__l_divs_end:

        .END
